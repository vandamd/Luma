apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

ktlint {
    version = "1.8.0"
    android = true
    verbose = true
    outputColorName = "RED"
}

def localProps = new Properties()
def localPropsFile = rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(new FileInputStream(localPropsFile))
}

android {
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    if (localProps['storeFile']) {
        signingConfigs {
            release {
                storeFile file(localProps['storeFile'])
                storePassword localProps['storePassword']
                keyAlias localProps['keyAlias']
                keyPassword localProps['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "app.luma"
        minSdk 31
        targetSdk 35
        versionCode 38
        versionName "v1.3.1"
    }

    buildFeatures {
        compose true
        viewBinding = true
        buildConfig = true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (localProps['storeFile']) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        abortOnError false
    }
    namespace 'app.luma'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.recyclerview:recyclerview:1.3.2"

    // Android lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.8.5"
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.5'

    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'

    // JETPACK Compose with BOM for version management
    implementation platform('androidx.compose:compose-bom:2024.12.01')
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.animation:animation'
    implementation 'androidx.compose.ui:ui'

    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0"
}
